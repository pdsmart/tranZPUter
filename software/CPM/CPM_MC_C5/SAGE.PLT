 R: FILE: SAGE.PLT
 R: by ELLEN NOLD and SALLY CANNON;  STANFORD 7/73
 R: Rewritten for PILOT/P by Ray Penley, 01/11/84
 T: Hi. What's your name?
 A: $n
 T: O.K., @$n@, this time we're going to talk
  : about what you can and can't see, hear, touch,
  : taste and feel. You just touched the computer
  : terminal. How does it feel?
 A: $a
*FINE
 T: Fine. Now say it again in a different way.
 T: It feels like -
 A: $b
 T: Good.  Even more far out? It feels as if -
 A: $c
 T: You've just written a poem.  Here it is:
  :
  :
  :      TERMINAL
  :
  :     @$a@
  :     Like @$b@
  :     Just as if @$c@
  :
  :
  :
  : Do you want to change your poem?
 A:
 M:Y!y!OK!ok
JY: *FINE
 T: I thought your poem was O.K., too.
  : Now think about the things you normally can't
  : see, hear, touch, taste or feel--like love, or
  : hate, or joy or desperation. Can you point to
  : "desperation" the same way you can point to a
  : desperate man?
 A:
 T: Well, you can describe desperation, but it would
  : be hard to point to. A poet can touch an abstract
  : idea like desperation with words.
  : Desperation feels like -
 A: $d
 T: Yeah.  And desperation tastes like -
 A: $e
 T: Oh?  Desperation looks and smells -
 A: $j
 T: That was harder, but now we can see, smell,
 T: taste and touch desperation. Want to go on?
 A:
 M:N!n
JY: *QUIT
 T: Good.  Now, @$n@, think of another abstract idea.
 A: $d
 T: What does @$d@ smell like?
 A: $e
 T: It smells like @$e@ ?
 T: Neat. What does @$d@ sound like?
 A: $f
 T: Sounds like @$f@ ?  Interesting.
 T: What does @$d@ look like?
 A: $g
 T: You could make a poem out of that.
  :
  :             @$d@
  :     BY @$n@
  :
  :             @$e@- @$d@
  :             @$f@- OH, @$d@
  :                @$g@
  :
  : Now, @$n@, would you like to take an
  : imaginary walk in the woods?
 A:
 M:N!n
TN:Good. It's autumn, and as you're walking through
TN:the woods, what colors do you see?
JY: *AA
 A: $h
 T:What do @$h@ remind you of?
 A:
 T:Uh huh. Walking in the woods can lead to thoughts
  :of abstract ideas such as death, change, beauty
  :or cycles. Think of some more abstracts and type
  :them in.
 A: $i
 T:How do you feel when you think about @$i@?
 A:
 T:O.K., would you like to write a short poem, @$n@?
 A:
 M:N!n
JY: *AB
 T:O.K.  A suggestion is to type four lines
  :using the @$h@ colors, and @$i@, your
  :abstract ideas, and the feelings you have about @$i@.
  :
  :         @$n@'S POEM
  :
 A:
 A:
 A:
 A: $j
 T:
 T:NICE, @$n@
*AA
 T:Here's something someone else wrote:
 T:
  :     One by one,
  :     branch by branch
  :     leaves topple,
  :     zigzag
  :     through motionless October
  :     struggle,
  :     founder--
  :     golden birds
  :     with broken wings.
  :
  :This poet makes the abstraction of "October"
  :touchable or more concrete by describing leaves.
  :To what does he compare leaves?
 A:
 M:B!b!F!f
TY:Yes, birds.
TN: Not exactly. How about "birds"?
 T:How do you think this poet felt about October?
 A:
 M:s!d!c!h!m!p!t!S!D!C!H!M!P!T
TY:I agree.
TN:Really? I didn't pick that up.
 T:Were your feelings about the woods at all similar
 T:to this poet's?
 A:
 M:N!n
TN:You see that your feelings are touchable, too.
TY:No? Well, even so, you can see that
TY:your feelings are touchable, too.
  :
  :Words can help us "touch" something abstract--
  :like a feeling or a season--though we can't
  :hold it in our hands.
  :
*AB
 T:Here, again are some of your words from this session:
  :
  : @$d@
  : @$b@
  : @$a@
  : @$f@
  : @$j@
  :
  :
  :If you'd like to do more with the abstract and
  :concrete, try CLOVE.  Bye for now, @$n@.
  :
  :
 E:
*QUIT
 T: O.K. @$n@
 T: Try SAGE again if you like.
 E:
